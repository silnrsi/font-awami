#
#	File: collfix.feax
#
# FEAX code for Awami Nastaliq

#	This file is part of the Awami Nastaliq font 
#	(https://software.sil.org/awami) and is 
#	Copyright (c) 2014-2024 SIL Global (https://www.sil.org/),
#	with Reserved Font Names "Awami" and "SIL".
#
#	This Font Software is licensed under the SIL Open Font License,
#	Version 1.1.
#
#	You should have received a copy of the license along with this Font Software.
#	If this is not the case, go to (https://openfontlicense.org/) for all the
#	details including an FAQ.

# This file is intended to be included in main.feax.

# Currently these lookups are run within feature calt. Eventually this might be changed to a stylistic set.

@CfMarkerNS = [cfNS0 cfN100 cfN200 cfN300 cfN400 cfN500 cfS100 cfS200 cfS300 cfS400 cfS500];
@CfMarkerEW = [cfEW0 cfE100 cfE200 cfE300 cfE400 cfE500 cfW100 cfW200 cfW300 cfW400 cfW500];
@CfMarker = [@CfMarkerNS @CfMarkerEW];

@Any = [@GDEF_bases @GDEF_marks];

lookup EndOfStreamKludge {
	# Ensure that the last nuqta has at least three slots after it.
	# This is needed for the insertion rules.
	ignore sub @Finals  @NuqtaLike' @Any @Any @Any;
	sub @Finals	 @NuqtaLike'  by  @NuqtaLike  bogus bogus bogus;
} EndOfStreamKludge;

lookup CollFixNuqtas {

	lookupflag UseMarkFilteringSet [@AllBases @NuqtaLike];

	# NB: lookups that follow @Any operate on the nuqta in the preceding slot, due to the earlier insertion. \/ \/ \/
																										#	 \/ \/ \/
	# beh + beh																							#	 \/ \/ \/

	sub absBehIni.beM2	@NuqtaLikeUpper' lookup _ShiftN100	absBehMed'		@NuqtaLikeUpper' 	@Any' lookup _ShiftS100;
	sub absBehIni.beM2	@NuqtaLikeUpper' lookup _ShiftN100	absBehMed.benn'	@NuqtaLikeUpper' 	@Any' lookup _ShiftS100;

	sub absBehIni.be	@DotTallL' 		 lookup _ShiftN100	absBehMed.qf'	@NuqtaLikeLower'	@Any' lookup _ShiftS300;
	sub absBehIni.be	@NuqtaLikeLower' lookup _ShiftN100	absBehMed.qf'	@NuqtaLikeLower'	@Any' lookup _ShiftS100;
	sub absBehIni.beM1	@NuqtaLikeLower' lookup _ShiftN200	absBehMed.be'	@NuqtaLikeLower'	@Any' lookup _ShiftS400;
	sub absBehIni.beM2	@NuqtaLikeLower' lookup _ShiftE100	absBehMed'		@NuqtaLikeLower'	@Any' lookup _ShiftS100;
	sub absBehIni.benn	@NuqtaLikeLower' lookup _ShiftN100	absBehMed.nn'	@NuqtaLikeLower'	@Any' lookup _ShiftS300;
	sub absBehIni.bere	@NuqtaLikeLower' lookup _ShiftN100	absBehMed.re'	@DotWideL'			@Any' lookup _ShiftS200W100;
	sub absBehIni.bere	@NuqtaLikeLower' lookup _ShiftN100	absBehMed.re'	@NuqtaLikeLower'	@Any' lookup _ShiftS200;
	sub absBehMed.bere	@NuqtaLikeLower' lookup _ShiftE200	absBehMed.re'	@NuqtaLikeLower' 	@Any' lookup _ShiftS400;


	# beh + beh + final jeem

	sub absBehIni.be	@DotFlatL' lookup _ShiftN200E300	absBehMed.jm'	@DotFlatL'	absJeemFin' @NuqtaLikeLower' lookup _ShiftS200;
	sub absBehIni.be	@DotTallL' lookup _ShiftN500E500	absBehMed.jm'	@DotFlatL'	absJeemFin' @NuqtaLikeLower' lookup _ShiftS200;
	sub absBehIni.be	@DotFlatL' lookup _ShiftN300E300	absBehMed.jm'	@DotTallL'	absJeemFin' @NuqtaLikeLower' lookup _ShiftS100;
	sub absBehIni.be	@DotTallL' lookup _ShiftN500E500	absBehMed.jm	@DotTallL	absJeemFin	@NuqtaLikeLower;
	# more below in CollFixNuqtas_contd

	# beh + kaf + heh-do
	sub absBehIni	@NuqtaLikeLower' lookup _ShiftS200E100		[absKehehMed.hd absGafMed.hd absKehehMed.hd_base absLamMed.hd];

	# beh + heh-goal
	sub absBehIni.hgM	@DotWideL' lookup _ShiftE200	@HehGoalMedForms	_hehHook.small;

	# medial jeem + final jeem

	sub absJeemMed.jm	@DotFlatL' lookup _ShiftE200	absJeemFin;
	sub absJeemMed.jm	@DotTallL' lookup _ShiftE300	absJeemFin;

	# jeem + qaf
	
	sub absJeemIni.qf	@NuqtaLike		absQafFin'	@NuqtaLikeUpper' lookup _ShiftW200;
	sub absJeemIni.qf					absQafFin'	@NuqtaLikeUpper' lookup _ShiftW200;

	# feh

	sub absFehIni.hd	@NuqtaLikeLower' lookup _ShiftE200	@HehDoForms;
	sub absFehIni.jm	@NuqtaLikeLower' lookup _ShiftE200	@JeemForms;

	sub absFehIni.beM1	@NuqtaLikeLower' lookup _ShiftN100	absBehMed.be' 		@NuqtaLikeLower'	@Any' lookup _ShiftS200;
	sub absFehIni.hgM	@NuqtaLikeLower' lookup _ShiftE100	@HehGoalMedForms'	_hehHook.small'		@Any' lookup _ShiftW100;


} CollFixNuqtas;

lookup CollFixNuqtas_contd {
	lookupflag UseMarkFilteringSet [@AllBases @NuqtaLike];
	sub absBehIni.be	@NuqtaLikeLower		absBehMed.jm	@NuqtaLikeLower	absJeemFin	@DotTallL' lookup _ShiftS200;
	sub absBehIni.be	@NuqtaLikeLower		absBehMed.jm	@DotTallL		absJeemFin	@NuqtaLikeLower' lookup _ShiftS200;
} CollFixNuqtas_contd;

lookup EndOfStreamUndokludge {
	sub bogus  by  NULL;
} EndOfStreamUndokludge;